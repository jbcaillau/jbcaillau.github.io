{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Dirichlet case (q = 1)\n",
    "\n",
    "The problem is formulated as follows: extremise $y(1)$ with ($A$ is the positive $L^1$ bound)\n",
    "\n",
    "$$ -y''(t)+V(t)y(t) = 0,\\quad \\|V\\|_1 \\leq A, $$\n",
    "\n",
    "$$ y(0)=0,\\quad y'(0)=1. $$\n",
    "\n",
    "Setting $x=(y,y',x_3)$ one equivalently extremises $x_1(1)$ s.t.\n",
    "\n",
    "$$ x_1'(t) = x_2(t),\\quad x_2'(t) = V(t)x_1(t),\\quad x_3'(t)=|V(t)|, $$\n",
    "\n",
    "$$ x(0)=(0,1,0),\\quad x_3(1) \\leq A. $$\n",
    "\n",
    "To avoid singularities, the control $V$ is decomposed into the difference of its positive and negative parts; and to ensure existence in the $L^1$ class, an atom at time $\\tau \\in (0,1)$ is assumed and the problem recast on $[0,\\tau]$ and $[\\tau,1]$ with suitable junction conditions."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP, Ipopt, Plots\n",
    "\n",
    "# JuMP model, Ipopt solver\n",
    "extdet = Model(optimizer_with_attributes(Ipopt.Optimizer, \"print_level\" => 5))\n",
    "\n",
    "# Parameters\n",
    "A = 23.7                      # L^1 bound on the potential\n",
    "t0 = 0.                       # initial time\n",
    "tf = 1.                       # final time\n",
    "N = 800                       # grid size\n",
    "Δt = (tf-t0)/N                # step size\n",
    "ε = -1.                       # sign of impulse\n",
    "\n",
    "@variables(extdet, begin\n",
    "    0 ≤ τ ≤ tf                # atom position\n",
    "    0 ≤ Δ ≤ A                 # absolute value of atom amplitude\n",
    "    x1[1:2, 1:N]              # x1 = y\n",
    "    x2[1:2, 1:N]              # x2 = y'\n",
    "    x3[1:2, 1:N]              # x3 for L^q constraint\n",
    "    0 ≤ V1[1:2, 1:N]          # positive part of the potential (= control)\n",
    "    0 ≤ V2[1:2, 1:N]          # negative part of the potential (= control)\n",
    "end)\n",
    "\n",
    "# Objective\n",
    "@objective(extdet, Min, x1[2, N])\n",
    "\n",
    "# Initial conditions\n",
    "@constraints(extdet, begin\n",
    "    x1[1, 1] == 0.\n",
    "    x2[1, 1] == 1.\n",
    "    x3[1, 1] == 0.\n",
    "    x1[2, 1] == x1[1, N]\n",
    "  # x2[2, 1]: nonlinear, see below\n",
    "    x3[2, 1] == x3[1, N]\n",
    "    x3[2, N] ≤ A-Δ\n",
    "end)\n",
    "\n",
    "@NLconstraint(extdet, x2[2, 1] == x2[1, N] + ε*Δ*x1[1, N]) # atom at tau\n",
    "\n",
    "@constraint(extdet, 0.3 ≤ τ ≤ 0.7) #debug\n",
    "\n",
    "# Dynamics, Crank-Nicolson scheme\n",
    "for j in 1:N-1\n",
    "    @NLconstraints(extdet, begin\n",
    "        x1[1, j+1] == x1[1, j] + 0.5 * Δt *    τ  * (x2[1, j] + x2[1, j+1])\n",
    "        x1[2, j+1] == x1[2, j] + 0.5 * Δt * (1-τ) * (x2[2, j] + x2[2, j+1])\n",
    "        x2[1, j+1] == x2[1, j] + 0.5 * Δt *    τ  * ((V1[1, j]-V2[1, j])*x1[1, j] + (V1[1, j+1]-V2[1, j+1])*x1[1, j+1])\n",
    "        x2[2, j+1] == x2[2, j] + 0.5 * Δt * (1-τ) * ((V1[2, j]-V2[2, j])*x1[2, j] + (V1[2, j+1]-V2[2, j+1])*x1[2, j+1])\n",
    "        x3[1, j+1] == x3[1, j] + 0.5 * Δt *    τ  * ((V1[1, j]+V2[1, j]) + (V1[1, j+1]+V2[1, j+1]))\n",
    "        x3[2, j+1] == x3[2, j] + 0.5 * Δt * (1-τ) * ((V1[2, j]+V2[2, j]) + (V1[2, j+1]+V2[2, j+1]))\n",
    "    end)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Solve for the control and state\n",
    "println(\"Solving...\")\n",
    "status = optimize!(extdet)\n",
    "\n",
    "# Display results\n",
    "println(\"Solver status: \", status)\n",
    "println(\"Cost: \", objective_value(extdet))\n",
    "println(\"τ = \", value.(τ))\n",
    "println(\"Δ = \", value.(Δ))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Plots\n",
    "\n",
    "tt  = vcat(value.(τ)*Δt*(1:N), value.(τ) .+ value.(1-τ)*Δt*(1:N))\n",
    "xx1 = vcat(value.(x1)[1, :], value.(x1)[2, :])\n",
    "xx2 = vcat(value.(x2)[1, :], value.(x2)[2, :])\n",
    "xx3 = vcat(value.(x3)[1, :], value.(x3)[2, :])\n",
    "VV1 = vcat(value.(V1)[1, :], value.(V1)[2, :])\n",
    "VV2 = vcat(value.(V2)[1, :], value.(V2)[2, :])\n",
    "x1_plot = plot(tt, xx1, xlabel = \"t\", ylabel = \"y\",  label = \"\")\n",
    "x2_plot = plot(tt, xx2, xlabel = \"t\", ylabel = \"y'\", label = \"\")\n",
    "display(plot(x1_plot, x2_plot, layout = (2, 1)))\n",
    "V_plot  = plot(tt, VV1-VV2, xlabel = \"t\", ylabel = \"V\",  label = \"\")\n",
    "V1_plot = plot(tt, VV1,     xlabel = \"t\", ylabel = \"V1\", label = \"\")\n",
    "V2_plot = plot(tt, VV2,     xlabel = \"t\", ylabel = \"V2\", label = \"\")\n",
    "display(plot(V_plot, V1_plot, V2_plot, layout = (3, 1)))"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.3",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}

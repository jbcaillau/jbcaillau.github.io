{
 "cells": [
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Waste-to-energy problem (julia)\n",
    "\n",
    "\n",
    "\n",
    "$$ \n",
    "\\left \\{\n",
    "\t\t\\begin{array}{lll}\n",
    "\t\t\t\\textcolor{white}{\\dot{x}(t)} = \\omega - \\big(\\beta + \\textcolor{white}{K(t)}\\textcolor{red}{q(t)} \\big) \\textcolor{white}{x(t)} \\\\[2mm]\n",
    "\t\t\t\\textcolor{white}{\\dot{K}(t)} = \\textcolor{red}{I(t)} - \\gamma \\textcolor{white}{K(t)}\\\\[2mm]\n",
    "\t\t\t\\textcolor{white}{\\dot{E}(t)} = \\mu \\textcolor{white}{K(t)x(t)}\\textcolor{red}{q(t)} -   (\\alpha + n \\textcolor{white}{K(t)}) \\textcolor{white}{E(t)}\n",
    "\t\t\\end{array}\n",
    "\\right.\t\n",
    "$$\n",
    "\n",
    "$\\textbf{[OCP]}$ Find admissible controls $\\textcolor{red}{(I,q)}$ maximizing,\n",
    "$$\n",
    "\t\tJ : =\\int_{0}^{\\textcolor{magenta}{T}}  e^{-\\delta t} \\textcolor{violet}{\\mathcal{J}(t)} dt\n",
    "$$\n",
    "$$\n",
    "\t    \\textcolor{violet}{\\mathcal{J}(t) = p \\textcolor{white}{E(t)} - c \\textcolor{white}{K(t)x(t)}\\textcolor{red}{q(t)} - \\textcolor{red}{I(t)}(c_1 + c_2 \\textcolor{red}{I(t)})}\n",
    "$$\n",
    "\n",
    "over a finite fixed-time horizon $[0,T]$,  with $ \\delta,p,c,c_1,c_2 \\geq 0$."
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Direct Solve"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP, Ipopt, Plots, MINPACK\n",
    "#JuMP model, Ipopt solver\n",
    "sys = Model(optimizer_with_attributes(Ipopt.Optimizer,\"print_level\"=> 5))\n",
    "set_optimizer_attribute(sys,\"tol\",1e-10)\n",
    "set_optimizer_attribute(sys,\"constr_viol_tol\",1e-10)\n",
    "set_optimizer_attribute(sys,\"max_iter\",5000)\n",
    "\n",
    "# Model Parameters\n",
    "w       = 50.0 \n",
    "b       = 0.25\n",
    "a       = 0.1\n",
    "g       = 0.2\n",
    "d       = 0.1\n",
    "mu      = 0.8\n",
    "n       = 0.015  # n can be zero\n",
    "p       = 1.0\n",
    "c       = 2.0  \n",
    "c1      = 2.0\n",
    "c2      = 3.0\n",
    "N       = 1000\n",
    "t0      = 0.0\n",
    "tf      = 10.0 \n",
    "x0      = 1.0\n",
    "k0      = 1.0\n",
    "E0      = 0.0\n",
    "cr0     = 0.0\n",
    "Imax    = 5.0  # Upper bound on control 1\n",
    "qmax    = 1.0  # Upper bound on control 2\n",
    "\n",
    "xi0 = [ x0, k0, E0, cr0]                #initial conditions IC\n",
    "dt=tf/N;                                #defining time-step\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Variables (some state constraints have been added to ease convergence)\n",
    "@variables(sys, begin\n",
    "    x[1:N+1] >= 0.0                     #x\n",
    "    k[1:N+1] >= 0.0                     #k\n",
    "    E[1:N+1] >= 0.0                     #E\n",
    "    cr[1:N+1]                           #Extra-state \n",
    "    0.0<= q[1:N+1]<= qmax               #q\n",
    "    0.0<= I[1:N+1]<= Imax               #I\n",
    "end)\n",
    "\n",
    "# Objective\n",
    "@objective(sys, Max, cr[N+1])           #maximize the criteria over a finite time-horizon \n",
    "#Remark: maximize the final-state of the extra-state Cr(Tf)=Cr[N+1]\n",
    "\n",
    "\n",
    "# Boundary constraints \n",
    "@constraints(sys, begin\n",
    "    con_x0, x[1]    == x0\n",
    "    con_k0, k[1]    == k0\n",
    "    con_E0, E[1]    == E0\n",
    "    con_cr0, cr[1]  == cr0 \n",
    "end)\n",
    "\n",
    "# Dynamics\n",
    "@NLexpressions(sys, begin\n",
    "    dx[i= 1:N+1], w- (b+q[i]*k[i])*x[i]\n",
    "    dk[i= 1:N+1], I[i]-g*k[i]\n",
    "    dE[i= 1:N+1], mu*q[i]*x[i]*k[i] - (a+n*k[i])*E[i]\n",
    "    dcr[i= 1:N+1], exp(-d*dt*(i-1))*(p*E[i]-c*q[i]*x[i]*k[i]- I[i]*(c1+c2*I[i]))      \n",
    "end)\n",
    "\n",
    "# Crank-Nicolson scheme\n",
    "@NLconstraints(sys, begin\n",
    "    con_dx[i =1:N], x[i+1] == x[i] + dt * (dx[i] + dx[i+1])/2.0 \n",
    "    con_dk[i =1:N], k[i+1] == k[i] + dt * (dk[i] + dk[i+1])/2.0 \n",
    "    con_dE[i =1:N], E[i+1] == E[i] + dt * (dE[i] + dE[i+1])/2.0 \n",
    "    con_dcr[i =1:N], cr[i+1] == cr[i] + dt * (dcr[i] + dcr[i+1])/2.0 \n",
    "end);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Solver for the control and state for the direct method [OUTPUT PRINT]\n",
    "println(\"Solving...\")\n",
    "status = optimize!(sys)\n",
    "\n",
    "if termination_status(sys) == MOI.OPTIMAL\n",
    "    println(\"Solution is optimal\")\n",
    "elseif termination_status(sys) == MOI.LOCALLY_SOLVED\n",
    "    println(\"Local solution found\")\n",
    "elseif termination_status(sys) == MOI.TIME_LIMIT && has_values(sys)\n",
    "    println(\"Solution is suboptimal due to a time limit, but a primal solution is available\")\n",
    "else\n",
    "    error(\"The model was not solved correctly.\")\n",
    "end\n",
    "println(\"Objective value = \", objective_value(sys), \"\\n\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#Optimal state trajectories, optimal co-states and optimal controls I*,q*\n",
    "t   = (1:N+1)*dt; t = (t[1:end-1] + t[2:end])/2.0\n",
    "xx  = value.(x); xx = (xx[1:end-1] + xx[2:end])/2.0\n",
    "kk  = value.(k); kk = (kk[1:end-1] + kk[2:end])/2.0\n",
    "EE  = value.(E); EE = (EE[1:end-1] + EE[2:end])/2.0\n",
    "crr = value.(cr); crr = (crr[1:end-1] + crr[2:end])/2.0\n",
    "qq  = value.(q); qq = (qq[1:end-1] + qq[2:end])/2.0\n",
    "II  = value.(I); II= (II[1:end-1] + II[2:end])/2.0\n",
    "xi  =  [ [ xx[i], kk[i], EE[i], crr[i] ] for i in 1:N ]\n",
    "pp  = -[ [dual(con_dx[i]),dual(con_dk[i]), dual(con_dE[i]), dual(con_dcr[i]) ] for i in 1:N ];"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#plot the optimal states and costates\n",
    "\n",
    "x_plot = plot(t, [ xi[i][1] for i in 1:N ], xlabel = \"t\", ylabel = \"x\", legend = false)\n",
    "k_plot = plot(t, [ xi[i][2] for i in 1:N ], xlabel = \"t\", ylabel = \"k\", legend = false)\n",
    "e_plot = plot(t, [ xi[i][3] for i in 1:N ], xlabel = \"t\", ylabel = \"E\", legend = false)\n",
    "cr_plot = plot(t, [ xi[i][4] for i in 1:N ], xlabel = \"t\", ylabel = \"cr\", legend = false)\n",
    "px_plot = plot(t, [ pp[i][1] for i in 1:N ], xlabel = \"t\", ylabel = \"px\", legend = false)\n",
    "pk_plot = plot(t, [ pp[i][2] for i in 1:N ], xlabel = \"t\", ylabel = \"pk\", legend = false)\n",
    "pe_plot = plot(t, [ pp[i][3] for i in 1:N ], xlabel = \"t\", ylabel = \"pE\", legend = false)\n",
    "pcr_plot = plot(t, [ pp[i][4] for i in 1:N ], xlabel = \"t\", ylabel = \"pcr\", legend = false)\n",
    "xi_plot = plot(x_plot, px_plot, k_plot, pk_plot, e_plot, pe_plot,cr_plot,pcr_plot, layout = (4,2),size=(800, 1200))\n",
    "display(xi_plot)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "############ Some extra-plots of key functions : Optimal control analysis\n",
    "function hdag(t,lmd)\n",
    "    Is= (exp(d*t)*lmd[2]-c1)/ (2*c2)\n",
    "    return Is\n",
    "end\n",
    "function swq(t,lmd)\n",
    "    sw= - c - exp(d*t)*lmd[1] + mu* exp(d*t)*lmd[3]        #Pseudoswitch_function\n",
    "    return sw\n",
    "end\n",
    "q_plot = plot(t, qq, xlabel = \"t\", ylabel = \"q\", legend = false)\n",
    "I_plot = plot(t, II, xlabel = \"t\", ylabel = \"I\", legend = false)\n",
    "sw_plot= plot(t, swq.(t,pp), xlabel = \"t\", ylabel = \"switch function\", legend = false)\n",
    "hdag_plot= plot(t, hdag.(t,pp), xlabel = \"t\", ylabel = \"hdag\", legend = false)\n",
    "\n",
    "plot(sw_plot,hdag_plot,q_plot,I_plot,layout = (2,2),size=(800, 400))"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The current value Hamiltonian:\n",
    "\n",
    "$$\n",
    "\\begin{array}{lll}\n",
    "    \\textcolor{nred}{H =} & \\textcolor{nred}{h(X,\\Lambda) + \\tilde{h} Kx \\textcolor{cyan}{q} + h^\\dag(\\textcolor{cyan}{I})}, \\\\\n",
    "\\end{array}\n",
    "$$\n",
    "with,\n",
    "$$h(X,\\Lambda)= pE + \\lambda_1 (w-\\beta x) -\\lambda_2\\gamma K - \\lambda_3\\alpha E$$\n",
    "\n",
    "\n",
    "$$\\tilde{h} = - c - \\lambda_1 + \\mu\\lambda_3$$\n",
    "\n",
    "\n",
    "$$h^\\dag(I) = -I(c_1+c_2I) + \\lambda_2I$$\n",
    "\n",
    "The pseudo-costates are given by,\n",
    "\n",
    "\n",
    "$$\\left\\{\n",
    "\\begin{array}{lll}\n",
    "    & \\textcolor{nred}{\\dot{\\lambda}_1 = (\\delta+\\beta)\\lambda_1 -\\tilde{h}Kq} \\\\[2mm]\n",
    "    &  \\dot{\\lambda}_2 = (\\delta+\\gamma)\\lambda_2 -\\tilde{h}xq   \\\\[2mm]\n",
    "    &  \\textcolor{nred}{\\dot{\\lambda}_3 = (\\delta+\\alpha)\\lambda_3-p  } \n",
    "\\end{array}\n",
    "\\right.\n",
    "$$\n",
    "\n",
    "The transversality conditions are given by,\n",
    "\n",
    "$$\n",
    "    \\textcolor{white}{\\lambda_i(T)=0, \\:\\:\\: \\forall i=1,2,3}\n",
    "$$\n",
    "\n",
    "The optimal control $\\textcolor{nred}{I^*(t)}$ satisfies,\n",
    "$$\n",
    "    \\textcolor{nred}{I^*(t) =\n",
    "        \\left\\{\n",
    "        \\begin{array}{lll}\n",
    "             & 0 \\:\\:\n",
    "             \\text{if} \\: \\lambda_2(t) \\leq c_1\\\\[3mm]\n",
    "             & \\min \\left\\{ \\frac{\\lambda_2(t)-c_1}{2c_2}, I_{\\max} \\right\\} \\:\\: \\text{if} \\: \\lambda_2(t) > c_1\n",
    "        \\end{array}\n",
    "        \\right.}\n",
    "$$\n",
    "\n",
    "The optimal control \\textcolor{nred}{$q^*(t)$} satisfies,\n",
    "$$\n",
    "    \\textcolor{nred}{q^*(t) =\n",
    "        \\left\\{\n",
    "        \\begin{array}{ll}\n",
    "             & 0, \\:\\:\n",
    "             \\text{if} \\: \\tilde{h} = - c - \\lambda_1 + \\mu\\lambda_3 <0\\\\[1.5mm]\n",
    "             & q_{max} \\:\\:\n",
    "             \\text{if} \\: \\tilde{h} >0,\\\\[1.5mm]\n",
    "        \\end{array}\n",
    "        \\right.}\n",
    "$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "include(\"flow.jl\");"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#Hamiltonian def\n",
    "\n",
    "function h0(t,xi,lmd)                   #h0 is when both control are zero (i.e q=0 and I=0)\n",
    "    x, k, E, cr= xi\n",
    "    px, pk, pE, pcr= lmd\n",
    "    q   = 0.0\n",
    "    I   = 0.0\n",
    "    r   = pcr*exp(-d*t)*(p*E-c*q*x*k- I*(c1+c2*I))+px*(w-(b+q*k)*x)+pk*(I-g*k)+pE*(mu*q*x*k-(a+n*k)*E)\n",
    "    return r\n",
    "end\n",
    "\n",
    "function h10(t,xi,lmd)                   #h10 is when q=1 and I=0\n",
    "    x, k, E, cr= xi\n",
    "    px, pk, pE, pcr= lmd\n",
    "    q   = 1.0\n",
    "    I   = 0.0\n",
    "    r   = pcr*exp(-d*t)*(p*E-c*q*x*k- I*(c1+c2*I))+px*(w-(b+q*k)*x)+pk*(I-g*k)+pE*(mu*q*x*k-(a+n*k)*E)\n",
    "    return r\n",
    "end\n",
    "\n",
    "function h1s(t,xi,lmd)                    #h1s is when q=1 and I=Is           \n",
    "    x, k, E, cr= xi\n",
    "    px, pk, pE, pcr= lmd\n",
    "    q   = 1.0\n",
    "    I   = (exp(d*t)*pk-c1)/(2*c2)\n",
    "    r   = pcr*exp(-d*t)*(p*E-c*q*x*k- I*(c1+c2*I))+px*(w-(b+q*k)*x)+pk*(I-g*k)+pE*(mu*q*x*k-(a+n*k)*E)\n",
    "    return r\n",
    "end\n",
    "\n",
    "######################################################################################################\n",
    "f0      = Flow(h0);\n",
    "f10     = Flow(h10);\n",
    "f1s     = Flow(h1s);\n",
    "######################################################################################################\n",
    "\n",
    "#Shooting function\n",
    "\n",
    "function shoot(p0, ti, tq)  # p0 initial costate, ti: switching instant control-I, tq: switching instant control-q\n",
    "    xs,ps  = f1s(t0, xi0, p0, ti)\n",
    "    xq,ppq = f10(ti, xs, ps ,tq)\n",
    "    xf,pf  = f0(tq, xq, ppq, tf)\n",
    "    s      = zeros(eltype(p0),6)\n",
    "    s[1:4] = pf[1:4] - [0.0, 0.0, 0.0, 1.0]\n",
    "    s[5]   = hdag(ti,ps)  # hdag: (like)switching function in I\n",
    "    s[6]   = swq(tq,ppq) # swq : switching function in q\n",
    "    return s;\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "display(plot(q_plot, sw_plot, I_plot, hdag_plot, layout = (2,2)))\n",
    "η    = 1e-3\n",
    "p0   = pp[1]\n",
    "ti1  = t[hdag.(t, pp) .≤ η ]\n",
    "tq1  = t[swq.(t ,pp) .≤ η ]\n",
    "ti   = min(ti1...)\n",
    "tq   = min(tq1...)\n",
    "ξ    = [p0 ; ti ; tq]\n",
    "println(\"Initial guess:\\n\", ξ)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Solve\n",
    "foo(ξ)          = shoot(ξ[1:4], ξ[5], ξ[6])\n",
    "jfoo(ξ)         = ForwardDiff.jacobian(foo, ξ)\n",
    "foo!(s, ξ)      = ( s[:] = foo(ξ); nothing )\n",
    "jfoo!(js, ξ)    = ( js[:] = jfoo(ξ); nothing )\n",
    "\n",
    "println(\"Initial value of shooting:\\n\", foo(ξ), \"\\n\\n\")\n",
    "\n",
    "#nl_sol = fsolve(foo!, jfoo!, ξ, show_trace=true); println(nl_sol)\n",
    "nl_sol = fsolve(foo!, ξ, show_trace=true); println(nl_sol)\n",
    "\n",
    "# Retrieves solution\n",
    "if nl_sol.converged\n",
    "    p0 = nl_sol.x[1:4]\n",
    "    ti = nl_sol.x[5]\n",
    "    tq = nl_sol.x[6];\n",
    "else\n",
    "    error(\"Not converged\")\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Plots\n",
    "\n",
    "ode_sol = f1s((t0, ti), xi0, p0)\n",
    "tt0 = ode_sol.t\n",
    "xx0 = [ ode_sol[1:4, j] for j in 1:size(tt0, 1) ]\n",
    "pp0 = [ ode_sol[5:8, j] for j in 1:size(tt0, 1) ]\n",
    "qq0 = ones(size(tt0, 1))\n",
    "ii0= hdag.(tt0, pp0)\n",
    "###################################\n",
    "ode_sol = f10((ti, tq), xx0[end], pp0[end])\n",
    "tt1 = ode_sol.t\n",
    "xx1 = [ ode_sol[1:4, j] for j in 1:size(tt1, 1) ]\n",
    "pp1 = [ ode_sol[5:8, j] for j in 1:size(tt1, 1) ]\n",
    "qq1 = ones(size(tt1, 1))\n",
    "ii1= zeros(size(tt1, 1))\n",
    "\n",
    "########################################\n",
    "ode_sol = f0((tq, tf), xx1[end], pp1[end])\n",
    "tt2 = ode_sol.t\n",
    "xx2 = [ ode_sol[1:4, j] for j in 1:size(tt2, 1) ]\n",
    "pp2 = [ ode_sol[5:8, j] for j in 1:size(tt2, 1) ]\n",
    "qq2 = zeros(size(tt2,1))\n",
    "ii2= zeros(size(tt2,1));"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "tt=[tt0; tt1; tt2]\n",
    "xxi=[xx0; xx1; xx2]\n",
    "ppp=[pp0; pp1; pp2]\n",
    "qqq=[qq0;qq1; qq2]\n",
    "iii=[ii0; ii1; ii2]\n",
    "N=length(tt)\n",
    "\n",
    "xx_plot = plot(tt, [ xxi[i][1] for i in 1:N ], xlabel = \"t\", ylabel = \"x\", legend = false)\n",
    "kk_plot = plot(tt, [ xxi[i][2] for i in 1:N ], xlabel = \"t\", ylabel = \"k\", legend = false)\n",
    "ee_plot = plot(tt, [ xxi[i][3] for i in 1:N ], xlabel = \"t\", ylabel = \"E\", legend = false)\n",
    "crcr_plot = plot(tt, [ xxi[i][4] for i in 1:N ], xlabel = \"t\", ylabel = \"cr\", legend = false)\n",
    "qqq_plot = plot(tt, qqq, xlabel = \"t\", ylabel = \"qindirect\", legend = false)\n",
    "iii_plot = plot(tt, iii, xlabel = \"t\", ylabel = \"qindirect\", legend = false)\n",
    "ppx_plot = plot(tt, [ ppp[i][1] for i in 1:N ], xlabel = \"t\", ylabel = \"px\", legend = false)\n",
    "ppk_plot = plot(tt, [ ppp[i][2] for i in 1:N ], xlabel = \"t\", ylabel = \"pk\", legend = false)\n",
    "ppe_plot = plot(tt, [ ppp[i][3] for i in 1:N ], xlabel = \"t\", ylabel = \"pE\", legend = false)\n",
    "ppcr_plot = plot(tt, [ ppp[i][4] for i in 1:N ], xlabel = \"t\", ylabel = \"pcr\", legend = false)\n",
    "xxi_plot = plot(xx_plot, ppx_plot, kk_plot, ppk_plot, ee_plot, ppe_plot,crcr_plot, ppcr_plot, layout = (4,2),size=(800, 1200))\n",
    "display(xxi_plot)\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "sw2_plot= plot(tt, swq.(tt, ppp), xlabel = \"t\", ylabel = \"switch function\", legend = false)\n",
    "display(plot(sw2_plot,qqq_plot,layout=(2,1)))\n",
    "\n",
    "uI_plot= plot(tt, hdag.(tt, ppp), xlabel = \"t\", ylabel = \"hdag\", legend = false)\n",
    "display(plot(iii_plot,uI_plot,layout=(2,1)))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 70,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.8.5",
   "language": "julia",
   "name": "julia-1.8"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.5"
  },
  "orig_nbformat": 4
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
